import java.text.SimpleDateFormat

ext {
    baseVersion = '0.2.8'
}

subprojects {

    apply plugin: "java-library"
    apply plugin: "signing"
    apply plugin: "maven-publish"

    group = 'com.github.zakgof'
    version = baseVersion + '.' + (project.name - ~/^natives-/)
    
    def archivesBaseName = 'velvet-video-natives'
    def github = 'https://github.com/zakgof/' + archivesBaseName
       
    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        baseName = archivesBaseName
        from('..') {
          include 'scripts/**'
        }
        from('.') {
          include 'src/**'
        }
    }

    ext {
    
        descr = 'Java video encoding/decoding/muxing/demuxing library native components'
        pomConfig = {
            scm {
                connection 'scm:git:' + github + '.git'
                developerConnection 'scm:git:' + github + '.git'
                url github
            }
            licenses {
                license {
                    name project.licname
                    url project.licurl
                }
            }
            developers {
                developer {
                  id 'zakgof'
                  name 'Oleksandr Zakusylo'
                  email 'zakgof@gmail.com'
                }
            }
        }
    }
    
    task nativeVersion()  {
        doLast {
            new File(projectDir, "velvet-video-natives/version.inf").text = """
Version=${version}
Buildtime=${new SimpleDateFormat("dd-MM-yyyy HH:mm:ss").format(new Date())}
"""
        }
    }
    
    jar {
        dependsOn nativeVersion 
        baseName = archivesBaseName
        from('.') {
          include 'LICENSE.*'
          include 'velvet-video-natives/**'
        }
    }
    
    artifacts {
        archives sourcesJar
    }
    
    publishing {
        repositories {
            maven {
                name 'ossrh'
                url = baseVersion.endsWith('SNAPSHOT') ? "https://oss.sonatype.org/content/repositories/snapshots/" : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials(PasswordCredentials)
            }
        }
        publications {
            mavenJava(MavenPublication) {
                groupId group
                artifactId archivesBaseName
                artifact source: jar
                artifact sourcesJar
                version project.version
                pom.withXml {
                    println 'WITH XML ' + project
                    def root = asNode()
                    root.appendNode('description', project.descr)
                    root.appendNode('name', project.archivesBaseName)
                    root.appendNode('url', github + '.git')
                    root.children().last() + pomConfig
                }
            }
        }
    }
    
    signing {
        sign publishing.publications['mavenJava']
    }
   
}